Proud Ash Moth

High

# Use of For Loop in Public or External Functions

## File Location
- https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/user/UserManager.sol#L881-L932
- https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/user/UserManager.sol#L1078-L1096
- https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/user/UserManager.sol#L1102-L1122

## Summary
For loops used in public or external functions within Solidity smart contracts can cause high gas fees and are vulnerable to Denial of Service (DoS) attacks. This happens because an attacker can manipulate the gas used by the function, causing the function to run out of gas or making it too expensive to be called by other users.

## Vulnerability Detail
In some functions in the _'UserManager.sol'_ contract, there is the use of for loops in public or external functions, which are vulnerable to DoS attacks

## Impact
- High Gas Fees
- Denial of Service (DoS)
- Vulnerability to Attack

## Code Snippet
L881-L932
```solidity
    function updateLocked(address borrower, uint256 amount, bool lock) external onlyMarket {
        uint256 actualAmount = decimalScaling(amount, stakingTokenDecimal);
        uint96 remaining = (actualAmount).toUint96();
        uint96 innerAmount = 0;
        Staker storage staker;
        uint256 currTime = getTimestamp();

        uint256 vouchersLength = _vouchers[borrower].length;
        for (uint256 i = 0; i < vouchersLength; i++) {
            Vouch storage vouch = _vouchers[borrower][i];
            staker = _stakers[vouch.staker];

            staker.lockedCoinAge += _calcLockedCoinAge(currTime, vouch.locked, staker.lastUpdated, vouch.lastUpdated);

            vouch.lastUpdated = currTime.toUint64();
            if (lock) {
                // Look up the staker and determine how much unlock stake they
                // have available for the borrower to borrow. If there is 0
                // then continue to the next voucher in the array
                uint96 availableStake = staker.stakedAmount - staker.locked;
                uint96 lockAmount = _min(availableStake, vouch.trust - vouch.locked);
                if (lockAmount == 0) continue;
                // Calculate the actualAmount to add to the lock then
                // add the extra actualAmount to lock to the _stakers locked actualAmount
                // and also update the vouches locked actualAmount and lastUpdated timestamp
                innerAmount = _min(remaining, lockAmount);
                staker.locked += innerAmount;
                vouch.locked += innerAmount;
            } else {
                // Look up how much this vouch has locked. If it is 0 then
                // continue to the next voucher. Then calculate the actualAmount to
                // unlock which is the min of the vouches lock and what is
                // remaining to unlock
                uint96 locked = vouch.locked;
                if (locked == 0) continue;
                innerAmount = _min(locked, remaining);
                // Update the stored locked values and last updated timestamp
                staker.locked -= innerAmount;
                vouch.locked -= innerAmount;
            }

            remaining -= innerAmount;
            // If there is no remaining actualAmount to lock/unlock
            // we can stop looping through _vouchers
            if (remaining <= 0) break;
        }

        // If we have looped through all the available _vouchers for this
        // borrower and we still have a remaining amount then we have to
        // revert as there is not enough _vouchers to lock/unlock
        if (remaining > 0) revert LockedRemaining();
    }
```
L1078-L1096
```solidity
    function onRepayBorrow(address borrower, uint256 overdueTime) external {
        if (address(uToken) != msg.sender) revert AuthFailed();

        Vouch[] memory borrowerVouchers = _vouchers[borrower];
        uint256 vouchersLength = borrowerVouchers.length;
        Vouch memory vouch;
        uint256 currTime = getTimestamp();
        // assuming the borrower's already overdue, accumulating all his _vouchers' previous frozen coin age
        for (uint256 i = 0; i < vouchersLength; i++) {
            vouch = borrowerVouchers[i];
            if (vouch.locked == 0) continue;
            _frozenCoinAge[vouch.staker] += _calcFrozenCoinAge(
                currTime,
                vouch.locked,
                _stakers[vouch.staker].lastUpdated,
                overdueTime
            );
        }
    }
```
L1102-L1122
```solidity
    function batchUpdateFrozenInfo(address[] calldata stakerList) external whenNotPaused {
        uint256 stakerLength = stakerList.length;
        if (stakerLength == 0) revert InvalidParams();

        // update member's frozen amount and global frozen amount
        uint256 tmpTotalFrozen = _totalFrozen;
        address staker = address(0);
        for (uint256 i = 0; i < stakerLength; i++) {
            staker = stakerList[i];
            (, , uint256 memberTotalFrozen) = _getEffectiveAmounts(staker);

            uint256 memberFrozenBefore = _memberFrozen[staker];
            if (memberFrozenBefore != memberTotalFrozen) {
                _memberFrozen[staker] = memberTotalFrozen;
                tmpTotalFrozen = tmpTotalFrozen - memberFrozenBefore + memberTotalFrozen;
            }
        }
        _totalFrozen = tmpTotalFrozen;

        comptroller.updateTotalStaked(stakingToken, _totalStaked - _totalFrozen);
    }
```

## Tool used

Manual Review

## Recommendation
To reduce the risk of DoS attacks and optimize gas costs, it is recommended to avoid using for loops in public or external functions. Some strategies that can be implemented are:
1. Batch Processing
2. Limiting Array Size
3. Using Alternative Data Structures
4. Use of Internal Functions