Gigantic Pastel Bird

Medium

# Users can get vouched for (entrusted) maliciously by utilizing the ERC1155Voucher's onERC1155BatchReceived function which lacks proper access control checks

### Summary

Due to a missing `require(isValidToken[msg.sender], "!valid token");` check, the `ERC1155Voucher` is vulnerable to an attack targeting the vector of enabling trust for a particular user's account without an actual transfer of a trusted `ERC1155` token happening in reality.

### Root Cause

While the `onERC1155Received` function has a proper `isValidToken[msg.sender]` check...
```solidity
    /// @dev Handles the receipt of a single ERC1155 token type. This function is
    /// @param operator The address which initiated the transfer (i.e. msg.sender)
    /// @param from The address which previously owned the token
    /// @param id The ID of the token being transferred
    /// @param value The amount of tokens being transferred
    /// @param data Additional data with no specified format
    /// @return `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))` if transfer is allowed
    function onERC1155Received(
        address operator,
        address from,
        uint256 id,
        uint256 value,
        bytes calldata data
    ) external returns (bytes4) {
        require(isValidToken[msg.sender], "!valid token");
        _vouchFor(from);
        return bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"));
    }
```
..., the `onERC1155BatchReceived` function doesn't implement that check:
```solidity
    /// @dev Handles the receipt of a multiple ERC1155 token types.
    /// @param operator The address which initiated the batch transfer (i.e. msg.sender)
    /// @param from The address which previously owned the token
    /// @param ids An array containing ids of each token being transferred (order and length must match values array)
    /// @param values An array containing amounts of each token being transferred (order and length must match ids array)
    /// @param data Additional data with no specified format
    /// @return `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))` if transfer is allowed
    function onERC1155BatchReceived(
        address operator,
        address from,
        uint256[] calldata ids,
        uint256[] calldata values,
        bytes calldata data
    ) external returns (bytes4) {
        _vouchFor(from);
        return bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"));
    }
```

### To summarize:
- In the https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/peripheral/ERC1155Voucher.sol#L114C1-L130C6 function, there's currently no proper access control.
- To fix the problem, there should be the same `msg.sender` check as in this line in the non-batch hook function: https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/7ffe43f68a1b8e8de1dfd9de5a4d89c90fd6f710/union-v2-contracts/contracts/peripheral/ERC1155Voucher.sol#L109

### Internal pre-conditions

1. A malicious user can call the `onERC1155BatchReceived` function directly and no prerequisities are needed.

### External pre-conditions

None.

### Attack Path

As I mentioned in the internal pre-conditions section, a malicious user just has to call `onERC1155BatchReceived` function, passing their account's address *(or any other address they wish to nominate as an entrusted account)* as the
```solidity
        address operator,
        address from, // PASSING THE DESIRED ACCOUNT AS THE FROM PROPERTY
        uint256[] calldata ids,
        uint256[] calldata values,
        bytes calldata data
```
...to forward the call to the...
```solidity
    /**
     *  @dev  Update the trust amount for existing members.
     *  @dev  Trust is the amount of the underlying token you would in theory be
     *        happy to lend to another member. Vouch is derived from trust and stake.
     *        Vouch is the minimum of trust and staked amount.
     *  Emits {LogUpdateTrust} event
     *  @param borrower Account address
     *  @param trustAmount Trust amount
     */
    function updateTrust(address borrower, uint96 trustAmount) external onlyMember(msg.sender) whenNotPaused {
        address staker = msg.sender;
   }
```
function

### Impact

A user can bypass the requirement and expenses of transferring validated ERC1155 tokens to the `ERC1155Voucher` contract, and still entrust any account he wishes as the `borrower` of the `ERC1155Voucher` contract.

### PoC

_No response_

### Mitigation

A recommended fix is adding the `isValidToken[msg.sender]` check to the `onERC1155BatchReceived` function, just like it is implemented in the single-receival hook function:
```diff
    /// @dev Handles the receipt of a multiple ERC1155 token types.
    /// @param operator The address which initiated the batch transfer (i.e. msg.sender)
    /// @param from The address which previously owned the token
    /// @param ids An array containing ids of each token being transferred (order and length must match values array)
    /// @param values An array containing amounts of each token being transferred (order and length must match ids array)
    /// @param data Additional data with no specified format
    /// @return `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))` if transfer is allowed
    function onERC1155BatchReceived(
        address operator,
        address from,
        uint256[] calldata ids,
        uint256[] calldata values,
        bytes calldata data
    ) external returns (bytes4) {
+       require(isValidToken[msg.sender], "not a valid token!!!");
        _vouchFor(from);
        return bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"));
    }
```