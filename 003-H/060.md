Handsome Velvet Chimpanzee

High

# `repayBorrowWithERC20Permit` calls `_repayBorrowFresh` without scaling the `interest` input.

## Summary
`repayBorrowWithERC20Permit` calls `_repayBorrowFresh` without scaling the `interest` input which leads to wrong accounting by mixing scaled and un-scaled quantities.

## Vulnerability Detail
In `repayBorrowWithERC20Permit`, `_repayBorrowFresh` is called with `interest` as input:

[UErc20.sol#L8-L21](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UErc20.sol#L8-L21)
```solidity
    function repayBorrowWithERC20Permit(
        address borrower,
        uint256 amount,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external whenNotPaused {
        IERC20Permit erc20Token = IERC20Permit(underlying);
        erc20Token.permit(msg.sender, address(this), amount, deadline, v, r, s);


        if (!accrueInterest()) revert AccrueInterestFailed();
        uint256 interest = calculatingInterest(borrower);
        _repayBorrowFresh(msg.sender, borrower, decimalScaling(amount, underlyingDecimal), interest);
```

`interest` is calculated using `calculatingInterest`, which returns the reduced interest amount:

[UToken.sol#L568-L570](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UToken.sol#L568-L570)
```solidity
    function calculatingInterest(address account) public view override returns (uint256) {
        return decimalReducing(_calculatingInterest(account), underlyingDecimal);
    }
```
Therefore, in `_repayBorrowFresh` any comparison between a scaled value and `interest` is wrong and any value being updated using `interest` will be incorrectly updated.

[UToken.sol#L704-L718](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UToken.sol#L704-L718)
```solidity
        if (repayAmount >= interest) {
            // Interest is split between the reserves and the uToken minters based on
            // the reserveFactorMantissa When set to WAD all the interest is paid to teh reserves.
            // any interest that isn't sent to the reserves is added to the redeemable amount
            // and can be redeemed by uToken minters.
            toReserveAmount = (interest * reserveFactorMantissa) / WAD;
            toRedeemableAmount = interest - toReserveAmount;


            // Update the total borrows to reduce by the amount of principal that has
            // been paid off
            _totalBorrows -= (repayAmount - interest);


            // Update the account borrows to reflect the repayment
            accountBorrows[borrower].principal = borrowedAmount - repayAmount;
            accountBorrows[borrower].interest = 0;
```

## Impact
As this issue breaks the entire function integrity, I believe it to be of high severity.

For instance, since the calculated interests are not scaled, a user could repay a lot of interests with a very small value.

## Code Snippet
[UErc20.sol#L8-L21](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UErc20.sol#L8-L21)
[UToken.sol#L568-L570](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UToken.sol#L568-L570)
[UToken.sol#L704-L718](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UToken.sol#L704-L718)

## Tool used

Manual Review

## Recommendation
Change `_calculatingInterest` visibility to internal and call it in `UErc20` and `UDai`.

```diff
    function repayBorrowWithERC20Permit(
        address borrower,
        uint256 amount,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external whenNotPaused {
        IERC20Permit erc20Token = IERC20Permit(underlying);
        erc20Token.permit(msg.sender, address(this), amount, deadline, v, r, s);


        if (!accrueInterest()) revert AccrueInterestFailed();
-        uint256 interest = calculatingInterest(borrower);
+        uint256 interest = _calculatingInterest(borrower);
        _repayBorrowFresh(msg.sender, borrower, decimalScaling(amount, underlyingDecimal), interest);
```