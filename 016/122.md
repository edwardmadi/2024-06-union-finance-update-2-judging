Quaint Golden Gecko

Medium

# UToken amount to be burned on redeem can be less than the corresponding amount withdrawn

## Summary

The amount of the UToken to be burned on redeem is rounded down. This happens after actual withdrawal and allows redeeming user to receive extra funds. This can be run multiple times in L2 environment, accumulating material profit whenever the associated gas costs are lower than the corresponding rounding in favor of the caller.

## Vulnerability Detail

When `exchangeRate` figure accumulates enough value to imply a significant rounding, it can be exploited via having less UTokens burnt than underlying tokens received. While the most important zero UToken amount case is correctly controlled for, the partial loss of precision case is not, enabling the surface of attacker supplying a significant number of the smallest possible dust redeem calls each receiving slightly more value than was burned.

## Impact

Attacker can drain value directly from the UToken when `exchangeRate` is big enough, while gas costs are low enough to have the rounding effect exceeding the cumulative costs even slightly. When this happens it can be possible to drain a significant share of the holdings as there is no direct limitation on the number of calls, i.e. the enabling conditions do not change after each of them.

## Code Snippet

There are no minimum redeem amount:

[UToken.sol#L845-L859](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UToken.sol#L845-L859)

```solidity
    function redeem(uint256 amountIn, uint256 amountOut) external override whenNotPaused nonReentrant {
        if (!accrueInterest()) revert AccrueInterestFailed();
        if (amountIn != 0 && amountOut != 0) revert AmountError();
        if (amountIn == 0 && amountOut == 0) revert AmountZero();

        uint256 exchangeRate = _exchangeRateStored();

        // Amount of the underlying token to redeem
        uint256 underlyingAmount = amountOut;

        if (amountIn > 0) {
            // We calculate the exchange rate and the amount of underlying to be redeemed:
            // underlyingAmount = amountIn x _exchangeRateStored
            underlyingAmount = decimalReducing((amountIn * exchangeRate) / WAD, underlyingDecimal);
        }
```

And amount to burn from the redeeming user is scaled down:

[UToken.sol#L867-L872](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UToken.sol#L867-L872)

```solidity
        uint256 actualAmount = decimalScaling(underlyingAmount - remaining, underlyingDecimal);
>>      uint256 realUtokenAmount = (actualAmount * WAD) / exchangeRate;
        if (realUtokenAmount == 0) revert AmountZero();
        _burn(msg.sender, realUtokenAmount);

        _totalRedeemable -= actualAmount;
```

While zero burn case is controlled for, the dust burn cases (having exchange rate of `2`, ask for and receive `3.9` units of underlying, burn `3.9 / 2 = 1` UTokens) are still possible.

## Tool used

Manual Review

## Recommendation

Consider scaling up the UToken amount needed, e.g.:

[UToken.sol#L867-L872](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UToken.sol#L867-L872)

```diff
        uint256 actualAmount = decimalScaling(underlyingAmount - remaining, underlyingDecimal);
-       uint256 realUtokenAmount = (actualAmount * WAD) / exchangeRate;
+       uint256 realUtokenAmount = (actualAmount * WAD + exchangeRate - 1) / exchangeRate;
        if (realUtokenAmount == 0) revert AmountZero();
        _burn(msg.sender, realUtokenAmount);

        _totalRedeemable -= actualAmount;
```