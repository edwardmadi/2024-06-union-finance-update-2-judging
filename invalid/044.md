Chilly Bone Hare

Medium

# Potential DOS in Controller.sol Contract  Inheriting `UUPSUpgradeable.sol`

## Summary
The controller  contract are deployed using a proxy pattern whereby the implementation contract is used by the proxy contract for all its logic. The proxy contract will make delegate calls to the implementation contract. This helps to facilitate future upgrades by pointing the proxy contract to a new and upgraded implementation contract.

However, if the implementation contract is left uninitialized, it is possible for any user to gain ownership of the `onlyOwner` role in the implementation contract for Controller.sol. Once the user has ownership they are able to perform an upgrade of the implementation contract's logic contract and delegate call into any arbitrary contract, allowing them to self-destruct the proxy's implementation contract. Consequently, this will prevent all Controller.sol interactions until a new implementation contract is deployed.
## Vulnerability Detail
The vulnerability stems from the use of the UUPS (Universal Upgradeable Proxy Standard) pattern in the Controller.sol contract without proper initialization of the implementation contract. The key issues are:

The implementation contract is deployed separately from the proxy contract.
The implementation contract's constructor is not called when the proxy is deployed.
The initialize() function, which typically sets up the initial state (including the owner), is not automatically called.
The implementation contract's initialize() function is likely to be unprotected, allowing anyone to call it.

This scenario allows an attacker to:

Identify the uninitialized implementation contract.
Call the initialize() function on the implementation contract directly.
Gain ownership rights on the implementation contract.
Use the newly acquired ownership to upgrade the implementation to a malicious contract.
Potentially execute destructive actions through the proxy, such as self-destructing the implementation.
## Impact

Denial of Service (DoS): An attacker can render the entire Controller.sol functionality unusable by self-destructing the implementation contract. This would break all interactions with the Controller until a new implementation is deployed.
Unauthorized Control: The attacker gains full control over the implementation contract, allowing them to modify its logic arbitrarily.
## Code Snippet
```solidity

//SPDX-License-Identifier: MIT
pragma solidity 0.8.16;

import {Initializable} from "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import {UUPSUpgradeable} from "@openzeppelin/contracts-upgradeable/proxy/utils/UUPSUpgradeable.sol";

/**
 * @title Controller component
 * @dev For easy access to any core components
 */
abstract contract Controller is Initializable, UUPSUpgradeable {
    /* -------------------------------------------------------------------
      Storage 
    ------------------------------------------------------------------- */

    /**
     * @dev The address of the admin
     */
    address public admin;

    /**
     * @dev The address of the pending admin
     */
    address public pendingAdmin;

    /**
     * @dev Is the contract paused
     */
    bool private _paused;

    /**
     * @dev The address of the pause guardian
     */
    address public pauseGuardian;

    /* -------------------------------------------------------------------
      Errors 
    ------------------------------------------------------------------- */

    error Paused();
    error NotPaused();
    error SenderNotAdmin();
    error SenderNotGuardian();
    error SenderNotPendingAdmin();

    /* -------------------------------------------------------------------
      Events 
    ------------------------------------------------------------------- */

    /**
     * @dev Emitted when the pause is triggered by a pauser (`account`).
     */
    event LogPaused(address account);

    /**
     * @dev Emitted when the pause is lifted by a pauser (`account`).
     */
    event LogUnpaused(address account);

    /* -------------------------------------------------------------------
      Modifiers 
    ------------------------------------------------------------------- */

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     */
    modifier whenNotPaused() {
        if (_paused) revert Paused();
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     */
    modifier whenPaused() {
        if (!_paused) revert NotPaused();
        _;
    }

    modifier onlyAdmin() {
        if (admin != msg.sender) revert SenderNotAdmin();
        _;
    }

    modifier onlyGuardian() {
        if (pauseGuardian != msg.sender) revert SenderNotGuardian();
        _;
    }

    /* -------------------------------------------------------------------
      Constructor/Initializer 
    ------------------------------------------------------------------- */

    //When using minimal deploy, do not call initialize directly during deploy, because msg.sender is the proxyFactory address, and you need to call it manually
    function __Controller_init(address admin_) internal onlyInitializing {
        _paused = false;
        admin = admin_;
        __UUPSUpgradeable_init();
        pauseGuardian = admin_;
    }

    /* -------------------------------------------------------------------
      Core Functions 
    ------------------------------------------------------------------- */

    function _authorizeUpgrade(address) internal view override onlyAdmin {}

    /**
     * @dev Check if the address provided is the admin
     * @param account Account address
     */
    function isAdmin(address account) external view returns (bool) {
        return account == admin;
    }

    /**
     * @dev set new admin account
     * @param account Account address
     */
    function setPendingAdmin(address account) external onlyAdmin {
        pendingAdmin = account;
    }

    function acceptAdmin() external {
        if (pendingAdmin != msg.sender) revert SenderNotPendingAdmin();
        admin = pendingAdmin;
    }

    /**
     * @dev Set pauseGuardian account
     * @param account Account address
     */
    function setGuardian(address account) external onlyAdmin {
        pauseGuardian = account;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() external view returns (bool) {
        return _paused;
    }

    /**
     * @dev Called by a pauser to pause, triggers stopped state.
     */
    function pause() external onlyGuardian whenNotPaused {
        _paused = true;
        emit LogPaused(msg.sender);
    }

    /**
     * @dev Called by a pauser to unpause, returns to normal state.
     */
    function unpause() external onlyGuardian whenPaused {
        _paused = false;
        emit LogUnpaused(msg.sender);
    }

    uint256[50] private ______gap;
}
```

https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/Controller.sol#L1-#L161
## Tool used

Manual Review

## Recommendation
Use a Factory Pattern: Implement a factory contract that deploys both the proxy and the implementation, and initializes the implementation in a single atomic transaction.