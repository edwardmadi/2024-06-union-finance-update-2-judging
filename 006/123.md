Quaint Golden Gecko

High

# `repayBorrowWithERC20Permit()` functions use incorrectly scaled accumulated interest value

## Summary

UDai's and UErc20's `repayBorrowWithERC20Permit()` functions supply `interest` figure scaled with incorrect decimals (underlying dp instead of internal dp) to `_repayBorrowFresh()`.

## Vulnerability Detail

`_repayBorrowFresh()` expects the interest value to be internal 18 dp scaled, while both UDai's and UErc20's `repayBorrowWithERC20Permit()` functions receive `interest` via calling `calculatingInterest()` that performs `decimalReducing()` on the total interest value obtained with `_calculatingInterest()`, so both effectively supply dust amount instead of interest whenever underlying decimals are lower than 18 dp.

The `interest` value is then used within `_repayBorrowFresh()` as borrower's full interest for updating the accounting. 

## Impact

As result any borrower can zero their accumulated interest nearly for free, paying only gas and some dust amount, with the help of `repayBorrowWithERC20Permit()`. I.e. all the borrowers can effectively avoid paying any interest.

## Code Snippet

`calculatingInterest(borrower)` returns 6 dp values:

[UToken.sol#L568-L570](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UToken.sol#L568-L570)

```solidity
    function calculatingInterest(address account) public view override returns (uint256) {
>>      return decimalReducing(_calculatingInterest(account), underlyingDecimal);
    }
```

This way `interest` is 6 dp, while `_repayBorrowFresh()` expects all arguments to be in the internal 18 dp:

[UDai.sol#L8-L25](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UDai.sol#L8-L25)

```solidity
contract UDai is UToken, IUDai {
    function repayBorrowWithPermit(
        ...
    ) external whenNotPaused {
        IDai erc20Token = IDai(underlying);
        erc20Token.permit(msg.sender, address(this), nonce, expiry, true, v, r, s);

        if (!accrueInterest()) revert AccrueInterestFailed();
>>      uint256 interest = calculatingInterest(borrower);
>>      _repayBorrowFresh(msg.sender, borrower, decimalScaling(amount, underlyingDecimal), interest);
    }
}
```

[UErc20.sol#L7-L23](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UErc20.sol#L7-L23)

```solidity
contract UErc20 is UToken {
    function repayBorrowWithERC20Permit(
        ...
    ) external whenNotPaused {
        IERC20Permit erc20Token = IERC20Permit(underlying);
        erc20Token.permit(msg.sender, address(this), amount, deadline, v, r, s);

        if (!accrueInterest()) revert AccrueInterestFailed();
>>      uint256 interest = calculatingInterest(borrower);
>>      _repayBorrowFresh(msg.sender, borrower, decimalScaling(amount, underlyingDecimal), interest);
    }
}
```

In `_repayBorrowFresh()` `interest` is used for setting the borrower's `accountBorrows[borrower].interest` accumulator, which leads to effectively zeroing it out when low dp value is used (i.e. without the need to provide any material funds):

[UToken.sol#L704-L718](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UToken.sol#L704-L718)

```solidity
        if (repayAmount >= interest) {
            // Interest is split between the reserves and the uToken minters based on
            // the reserveFactorMantissa When set to WAD all the interest is paid to teh reserves.
            // any interest that isn't sent to the reserves is added to the redeemable amount
            // and can be redeemed by uToken minters.
            toReserveAmount = (interest * reserveFactorMantissa) / WAD;
            toRedeemableAmount = interest - toReserveAmount;

            // Update the total borrows to reduce by the amount of principal that has
            // been paid off
            _totalBorrows -= (repayAmount - interest);

            // Update the account borrows to reflect the repayment
>>          accountBorrows[borrower].principal = borrowedAmount - repayAmount;
>>          accountBorrows[borrower].interest = 0;
```

[UToken.sol#L744-L750](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UToken.sol#L744-L750)

```solidity
        } else {
            // For repayments that don't pay off the minimum we just need to adjust the
            // global balances and reduce the amount of interest accrued for the borrower
            toReserveAmount = (repayAmount * reserveFactorMantissa) / WAD;
            toRedeemableAmount = repayAmount - toReserveAmount;
>>          accountBorrows[borrower].interest = interest - repayAmount;
        }
```

## Tool used

Manual Review

## Recommendation

Consider either scaling `interest` value directly or using `_calculatingInterest()`, e.g.:

[UDai.sol#L8-L25](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UDai.sol#L8-L25)

```diff
contract UDai is UToken, IUDai {
    function repayBorrowWithPermit(
        ...
    ) external whenNotPaused {
        IDai erc20Token = IDai(underlying);
        erc20Token.permit(msg.sender, address(this), nonce, expiry, true, v, r, s);

        if (!accrueInterest()) revert AccrueInterestFailed();
-       uint256 interest = calculatingInterest(borrower);
+       uint256 interest = _calculatingInterest(borrower);
        _repayBorrowFresh(msg.sender, borrower, decimalScaling(amount, underlyingDecimal), interest);
    }
}
```

[UErc20.sol#L7-L23](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UErc20.sol#L7-L23)

```diff
contract UErc20 is UToken {
    function repayBorrowWithERC20Permit(
        ...
    ) external whenNotPaused {
        IERC20Permit erc20Token = IERC20Permit(underlying);
        erc20Token.permit(msg.sender, address(this), amount, deadline, v, r, s);

        if (!accrueInterest()) revert AccrueInterestFailed();
-       uint256 interest = calculatingInterest(borrower);
+       uint256 interest = _calculatingInterest(borrower);
        _repayBorrowFresh(msg.sender, borrower, decimalScaling(amount, underlyingDecimal), interest);
    }
}
```

[UToken.sol#L572](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/main/union-v2-contracts/contracts/market/UToken.sol#L572)

```diff
-   function _calculatingInterest(address account) private view returns (uint256) {
+   function _calculatingInterest(address account) internal view returns (uint256) {
```